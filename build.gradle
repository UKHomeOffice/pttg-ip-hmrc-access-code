buildscript {
  ext {
    springBootVersion = '2.0.0.RELEASE'
  }
  repositories {
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath 'net.researchgate:gradle-release:2.6.0'
  }
}

version = '0.0.1'

if (System.getenv('VERSION')) {
  version = System.getenv('VERSION')
}
if (System.getenv('BUILD_NUMBER')) {
  version = version + '-' + System.getenv('BUILD_NUMBER')
}


apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'net.researchgate.release'

springBoot {
  mainClassName = 'uk.gov.digital.ho.pttg.ServiceRunner'
}

checkstyleTest.enabled = false
checkstyle {
  ignoreFailures = false
}
findbugs {
  ignoreFailures = false
}

tasks.withType(FindBugs) {
  excludeFilter = file('config/findbugs/exclude-filter.xml')
  reports {
    xml.enabled false
    html.enabled true
  }
}


jar {
  baseName = 'pttg-ip-hmrc-access-code'
  if (System.getenv('GIT_COMMIT')) {
    version = version + '.' + System.getenv('GIT_COMMIT')
  }
  manifest {
    attributes 'Implementation-Title': baseName,
            'Implementation-Version': version
  }
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.16.12'

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  compile group: "org.springframework.boot", name: "spring-boot-configuration-processor"

  compile group: 'org.springframework.retry', name: 'spring-retry'

  compile group: 'com.fasterxml.jackson.datatype',  name: 'jackson-datatype-jsr310',  version: '2.8.3'
  compile group: 'org.apache.commons',              name: 'commons-lang3',            version: '3.0'
  compile group: 'net.logstash.logback',            name: 'logstash-logback-encoder', version: '5.0'

  compile group: 'commons-codec',             name: 'commons-codec',  version: '1.10'
  compile group: 'org.apache.httpcomponents', name: 'httpclient',     version: '4.5.3'
  compile group: 'org.flywaydb',              name: 'flyway-core',    version: '4.2.0'
  compile group: 'org.hsqldb',                name: 'hsqldb',         version: '2.4.0'
  compile group: 'org.postgresql',            name: 'postgresql',     version: '9.4.1211.jre7'

  compileOnly group: 'org.projectlombok',     name: 'lombok',         version: '1.16.12'

  testCompile group: 'org.springframework.boot',  name: 'spring-boot-starter-test'
  testCompile group: 'com.jayway.jsonpath',       name: 'json-path'
  testCompile group: 'com.google.code.findbugs',  name: 'annotations',    version: '3.0.1u2'
  testCompile group: 'junit',                     name: 'junit',          version: '4.12'
  testCompile group: 'org.assertj',               name: 'assertj-core',   version: '3.5.2'
  testCompile group: 'com.github.tomakehurst',    name: 'wiremock',       version: '2.6.0'
  testCompile group: 'org.apache.commons',        name: 'commons-io',     version: '1.3.2'

}


gradle.startParameter.continueOnFailure = false

release {
  git{
    requireBranch = 'EE-9726-semver-tagging'
  }

  preTagCommitMessage = '[Gradle Release Plugin] (EE-9726) - pre tag commit: '
  tagCommitMessage = '[Gradle Release Plugin] (EE-9726) - creating tag: '
  newVersionCommitMessage = '[Gradle Release Plugin] (EE-9726) - new version commit: '
}